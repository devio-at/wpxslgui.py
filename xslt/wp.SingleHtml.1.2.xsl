<?xml version="1.0" ?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="1.0"
			xmlns:content="http://purl.org/rss/1.0/modules/content/"
			xmlns:wp="http://wordpress.org/export/1.2/">
	<xsl:output method="html" doctype-public="-//W3C//DTD XHTML 1.0 Transitional//EN" doctype-system="http://www.w3.org/tr/xhtml1/DTD/xhtml1-transitional.dtd" 
		omit-xml-declaration="yes" media-type="text/html" />
	<xsl:template match="/rss/channel">
		<html>
			<head>
				<title>
					<xsl:value-of select="title"/> -
					<xsl:value-of select="description"/>
				</title>
				<style type="text/css">
* { font-family: Arial, Sans-Serif; }
body { font-size: smaller; }

.h1 { font-weight: bolder; color: blue; }
.h1h .items { display: none;  }
.items { margin-left: 1em; }

.h2 { cursor: pointer; color: blue; }
.h2h .content { display: none; }
.h2hs .content { border: solid 1px grey; margin: 2px; margin-left: 0em; padding: 2px }

table.title { padding: 5px; margin: 0px; border-collapse: collapse; }
table.title tr td { padding-right: 5px; }
table.detail { width: 100%; padding: 5px; margin: 0px; border-collapse: collapse; }
tr.header { background-color: silver; }
table.detail tr td { white-space: nowrap; font-size: smaller; }
pre { font-size: smaller; }
				</style>
				<script type="text/javascript">
function toggle(div)
{
	if (div.className=="h1h") { div.className="h1hs"; } else { div.className="h1h"; };
}
function toggledetail(div)
{
	if (div.className=="h2h") { div.className="h2hs"; } else { div.className="h2h"; };
}
				</script>
			</head>
			<body>
				<a target="_blank">
					<xsl:attribute name="href">
						<xsl:value-of select="link"/>
					</xsl:attribute>
					<h3>
						<xsl:value-of select="title"/>
						<br />
						<xsl:value-of select="description"/>
					</h3>
				</a>
				<p />
					<div class="items">
						<xsl:apply-templates select="item[wp:post_type = 'post' and wp:status = 'publish']"></xsl:apply-templates>
					</div>
				<p />
				<i>
					generated by <a href="https://www.devio.at/en/projects/wpxslgui/" target="_blank">devio wpxslgui</a>
				</i>
			</body>
		</html>
	</xsl:template>
	
	<xsl:template match="item" >
		<div class="h2h">
			<div class="h2" onclick="toggledetail(this.parentNode)">
				<xsl:attribute name="id">
					<xsl:value-of select="wp:post_id"/>
				</xsl:attribute>
				<xsl:value-of select="title"/>
			</div>
			<div class="content">
					<xsl:call-template name="break">
						<xsl:with-param name="text" select="content:encoded"></xsl:with-param>
					</xsl:call-template>
			</div>
			<div>
				<xsl:value-of select="wp:post_date"></xsl:value-of>
				<xsl:text> </xsl:text>
				<a target="_blank">
					<xsl:attribute name="href">
						<xsl:value-of select="link"/>
					</xsl:attribute>Link</a>
				<xsl:apply-templates select="category"></xsl:apply-templates>
			</div>
			<br />
		</div>
	</xsl:template>

	<xsl:template name="category" match="category">
		<xsl:if test="@nicename!=''">
			<xsl:text> </xsl:text>
			<xsl:value-of select="." />
		</xsl:if>
	</xsl:template>

	<xsl:template name="break">
		<xsl:param name="text" select="content:encoded"/>
		<xsl:choose>
			<xsl:when test="contains($text, '&#xa;')">
				<xsl:value-of select="substring-before($text, '&#xa;')"  disable-output-escaping="yes"/>
				<br/>
				<xsl:call-template name="break">
					<xsl:with-param name="text" select="substring-after($text, '&#xa;')"/>
				</xsl:call-template>
			</xsl:when>
			<xsl:otherwise>
				<xsl:value-of select="$text"  disable-output-escaping="yes"/>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>

</xsl:stylesheet>