<?xml version="1.0" ?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="1.0"
                xmlns:msxsl="urn:schemas-microsoft-com:xslt"  
			xmlns:content="http://purl.org/rss/1.0/modules/content/"
			xmlns:wp="http://wordpress.org/export/1.2/">
  <xsl:output method="html" doctype-public="-//W3C//DTD XHTML 1.0 Transitional//EN" doctype-system="http://www.w3.org/tr/xhtml1/DTD/xhtml1-transitional.dtd"
		omit-xml-declaration="yes" media-type="text/html" />
  <xsl:template match="/rss/channel">
    <html>
      <head>
        <title>
          Links in
          <xsl:value-of select="title"/> -
          <xsl:value-of select="description"/>
          (linked)
        </title>
        <style type="text/css">
          * { font-family: Arial, Sans-Serif; }
          body { font-size: smaller; }
          .items { margin-left: 1em; }
          .h2 { cursor: pointer; color: blue; }
          a.href { text-decoration: none; }
        </style>
      </head>
      <body>
        <h3>
          Links in
          <a target="_blank">
            <xsl:attribute name="href">
              <xsl:value-of select="link"/>
            </xsl:attribute>
            <xsl:value-of select="title"/>
            <br />
            <xsl:value-of select="description"/>
          </a>
          (linked)
        </h3>
        <p />
        <div class="items">
          <xsl:apply-templates select="item[wp:post_type = 'post' and wp:status = 'publish']"></xsl:apply-templates>
        </div>
        <p />
        <i>
          generated by <a href="https://www.devio.at/en/projects/wpxslgui/" target="_blank">devio wpxslgui</a>
        </i>
      </body>
    </html>
  </xsl:template>

  <xsl:template match="item" >
    <xsl:choose>
      <xsl:when test="contains(content:encoded, '&lt;a ')">
    <div class="h2h">
      <div class="h2" >
        <xsl:attribute name="id">
          <xsl:value-of select="wp:post_id"/>
        </xsl:attribute>
        <xsl:value-of select="title"/>
      </div>
      <div>
        <xsl:value-of select="wp:post_date"></xsl:value-of>
        <xsl:text> </xsl:text>
        <a target="_blank">
          <xsl:attribute name="href">
            <xsl:value-of select="link"/>
          </xsl:attribute>Link
        </a>
      </div>
      <div class="content">
        <xsl:call-template name="break">
          <xsl:with-param name="text" select="content:encoded"></xsl:with-param>
        </xsl:call-template>
      </div>
      <br />
    </div>
      </xsl:when>
      <xsl:otherwise>

      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>

  <xsl:template name="break">
    <xsl:param name="text" select="content:encoded"/>
    <xsl:choose>
      <xsl:when test="contains($text, '&lt;a ')">
        <xsl:variable name="before" select="substring-before($text, '&lt;a ')" />
        <xsl:variable name="aetc" select="substring($text, string-length($before) + 1)" />
        <xsl:variable name="after" select="substring-after($aetc, '&lt;/a&gt;')" />
        <xsl:variable name="a" select="substring($aetc, 1, string-length($aetc) - string-length($after))" />
        <xsl:variable name="hrefbefore" select="substring-before($a, 'href=&#34;')" />
        <xsl:variable name="hrefetc" select="substring($a, string-length($hrefbefore) + 7)" />
        <xsl:variable name="hrefafter" select="substring-after($hrefetc, '&#34;')" />
        <xsl:variable name="href" select="substring($hrefetc, 1, string-length($hrefetc) - string-length($hrefafter) - 1)" />
        <xsl:text>&#xD;&#xA;</xsl:text>
        <a class="href" target="_blank">
          <xsl:attribute name="href">
            <xsl:value-of select="$href"/>
          </xsl:attribute>
          <xsl:value-of select="$a"/>
        </a>
        <br/>
        <xsl:call-template name="break">
          <xsl:with-param name="text" select="$after"/>
        </xsl:call-template>
      </xsl:when>
      <xsl:otherwise>
        
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>

</xsl:stylesheet>